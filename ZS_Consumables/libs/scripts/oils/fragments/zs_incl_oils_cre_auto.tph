ACTION_IF zs_cons_allow_random_redistribution_cre BEGIN
    WITH_SCOPE BEGIN

        ACTION_PHP_EACH NPC_ARRAY AS file => dv BEGIN
            ACTION_IF IS_AN_INT dv BEGIN // non-joinable NPC
                COPY_EXISTING ~%file%~ ~override~
                    LPF IS_SUMMON RET is_summon = result END // not a summon either
                    LPF CAN_DROP_STUFF RET can_drop_stuff = result END // and has droppable items, i.e. can hold oil

                    PATCH_IF NOT is_summon AND can_drop_stuff BEGIN

                        READ_BYTE 0x273 class
                        PATCH_IF class = class_thief        OR
                                class = class_fighter_thief
                        BEGIN

                            SET chance = RANDOM(1 100)
                            SET chances_noxious = chance > (100 - zs_cons_thief_noxious_oil_assignment_chances)
                            SET chances_caustic = chance <= zs_cons_thief_caustic_oil_assignment_chances

                            PATCH_IF chances_noxious OR chances_caustic BEGIN

                                SET amount = RANDOM(1 3)
                                SET kit = SHORT_AT 0x246 | (SHORT_AT 0x244) << 16
                                PATCH_IF kit = kit_assassin BEGIN
                                    amount = amount * 2
                                END

                                PATCH_IF chances_noxious BEGIN

                                    ADD_CRE_ITEM ~ZSCSPS01~ (amount) (0) (0) ~none~ ~inv~ // poison oil
                                    SET chance_drow_poison = RANDOM(1 100)

                                    PATCH_IF chance_drow_poison <= zs_cons_thief_drow_poison_assignment_chances BEGIN
                                        ADD_CRE_ITEM ~ZSCSPS02~ (1) (0) (0) ~none~ ~inv~ // drow soporific oil
                                    END

                                END ELSE PATCH_IF chances_caustic BEGIN

                                    ADD_CRE_ITEM ~ZSCSAC01~ (amount) (0) (0) ~none~ ~inv~ // acid oil

                                END

                            END

                        END

                        ELSE PATCH_IF class = class_fighter OR
                                      class = class_ranger
                        BEGIN
                            SET kit = SHORT_AT 0x246 | (SHORT_AT 0x244) << 16
                            SET chance = RANDOM(1 100)
                            PATCH_IF chance <= zs_cons_fighter_elemental_assignment_chances BEGIN
                                SET oil_type = RANDOM(1 3)
                                SET amount = RANDOM(1 2)
                                PATCH_IF oil_type = 1 BEGIN
                                    ADD_CRE_ITEM ~ZSCSFR01~ (amount) (0) (0) ~none~ ~inv~
                                END ELSE PATCH_IF oil_type = 2 BEGIN
                                    ADD_CRE_ITEM ~ZSCSCD01~ (amount) (0) (0) ~none~ ~inv~
                                END ELSE PATCH_IF oil_type = 3 BEGIN
                                    ADD_CRE_ITEM ~ZSCSEL01~ (amount) (0) (0) ~none~ ~inv~
                                END
                            END
                        END

                        ELSE PATCH_IF class = class_cleric      OR
                                      class = class_paladin_all
                        BEGIN
                            READ_BYTE 0x27b alignment
                            PATCH_IF alignment = alignmen_chaotic_evil OR // PROFANE
                                    alignment  = alignmen_neutral_evil OR
                                    alignment  = alignmen_lawful_evil
                            BEGIN

                                SET kit = SHORT_AT 0x246 | (SHORT_AT 0x244) << 16
                                PATCH_IF kit = kit_blackguard BEGIN // poison OR profane
                                    SET chance = RANDOM(1 100)
                                    PATCH_IF chance <= 50 BEGIN
                                        ADD_CRE_ITEM ~ZSCSPS01~ (1) (0) (0) ~none~ ~inv~
                                    END
                                END

                                SET chance = RANDOM(1 100)
                                PATCH_IF chance <= 20 BEGIN
                                    ADD_CRE_ITEM ~ZSCSUL01~ (1) (0) (0) ~none~ ~inv~
                                END

                            END ELSE PATCH_IF alignment = alignmen_chaotic_neutral OR // chance of profane or sanctified
                                              alignment = alignmen_neutral         OR
                                              alignment = alignmen_lawful_neutral
                            BEGIN

                                SET chance = RANDOM(1 100)
                                PATCH_IF chance <= 50 BEGIN
                                    SET chance = RANDOM(1 100)
                                    PATCH_IF chance <= 15 BEGIN
                                        ADD_CRE_ITEM ~ZSCSHL01~ (1) (0) (0) ~none~ ~inv~
                                    END
                                END ELSE BEGIN
                                    SET chance = RANDOM(1 100)
                                    PATCH_IF chance <= 15 BEGIN
                                        ADD_CRE_ITEM ~ZSCSUL01~ (1) (0) (0) ~none~ ~inv~
                                    END
                                END

                            END ELSE PATCH_IF alignment = alignmen_chaotic_good OR // sanctified
                                              alignment = alignmen_neutral_good OR
                                              alignment = alignmen_lawful_good
                            BEGIN
                                SET chance = RANDOM(1 100)
                                PATCH_IF chance <= 20 BEGIN
                                    ADD_CRE_ITEM ~ZSCSHL01~ (1) (0) (0) ~none~ ~inv~
                                END
                            END

                        END

                        ELSE PATCH_IF class = class_mage         OR
                                      class = class_sorcerer     OR
                                      class = class_bard         OR
                                      class = class_fighter_mage OR
                                      class = class_mage_thief
                        BEGIN
                            SET chance = RANDOM(1 100)
                            PATCH_IF chance <= zs_cons_mage_arcane_oil_assignment_chances BEGIN
                                ADD_CRE_ITEM ~ZSCSMD01~ (1) (0) (0) ~none~ ~inv~
                            END
                        END
                    END
            END

        END

    END

END
