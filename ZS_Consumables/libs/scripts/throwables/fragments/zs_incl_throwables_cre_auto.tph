ACTION_IF zs_cons_add_random_redistribution_cre BEGIN
    SILENT
    WITH_SCOPE BEGIN

        ACTION_PHP_EACH NPC_ARRAY AS file => dv BEGIN
            ACTION_IF IS_AN_INT dv BEGIN // non-joinable NPC

                COPY_EXISTING ~%file%~ ~override~
                    LPF IS_SUMMON RET is_summon = result END // not a summon either
                    LPF CAN_DROP_STUFF RET can_drop_stuff = result END // and has droppable items, i.e. can hold oil

                    PATCH_IF NOT is_summon AND can_drop_stuff BEGIN

                        SET class = BYTE_AT(0x273)
                        PATCH_IF class = class_druid OR class = class_shaman BEGIN

                            SET chance_pixie = RANDOM(1 100)
                            PATCH_IF chance_pixie <= 5 BEGIN
                                ADD_CRE_ITEM ~zscspxd0~ (1) (0) (0) ~none~ ~inv4 inv5 inv6 inv7 inv8 inv9 inv10 inv11 inv12 inv13 inv14 inv15 inv16~ // pixie dust
                            END

                            SET chance_tanglefoot = RANDOM(1 100)
                            PATCH_IF chance_tanglefoot <= 12 BEGIN
                                SET amount = RANDOM(1 2)
                                ADD_CRE_ITEM ~zscstgf0~ (amount) (0) (0) ~none~ ~inv4 inv5 inv6 inv7 inv8 inv9 inv10 inv11 inv12 inv13 inv14 inv15 inv16~ // tanglefoot bag
                            END

                        END

                    END
                BUT_ONLY
            END

        END

    END
    VERBOSE
END
