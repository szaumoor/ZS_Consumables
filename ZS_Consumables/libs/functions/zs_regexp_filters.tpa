DEFINE_PATCH_FUNCTION IS_SUMMON
RET
    result
BEGIN
    LPF CHECK_EXTENSION STR_VAR extension = "CRE" END
    READ_BYTE 0x275 gender
    SET result = gender == gender_summoned       OR
                 gender == gender_summoned_demon OR
                 "%SOURCE_RES" STR_EQ "devaevil" OR
                 "%SOURCE_RES" STR_EQ "devagood" OR
                 "%SOURCE_RES" STR_EQ "planevil" OR
                 "%SOURCE_RES" STR_EQ "plangood"

END

DEFINE_PATCH_FUNCTION CAN_DROP_STUFF
RET
    result
BEGIN
    LPF CHECK_EXTENSION STR_VAR extension = "CRE" END
    READ_LONG 0x2c0 items_count
    SET result = 0
    PATCH_IF items_count > 0 BEGIN

        READ_LONG 0x2bc items_offset
        FOR ( i = 0; i < items_count; ++i ) BEGIN
            SET current_item_offset = items_offset + (i * 0x14)
            SET flags = LONG_AT (current_item_offset + 0x10)
            LPF IS_BIT_SET INT_VAR bit = 3 bit_field = flags RET undroppable = result END
            PATCH_IF NOT undroppable BEGIN
                SET result = 1
                i = items_count // break the loop
            END
        END

    END ELSE BEGIN
        SET result = 1
    END
END
